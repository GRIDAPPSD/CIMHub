Clear

! Modified Kersting YDY Test
! Impedances of the transformer changed per Roger Dugan
! Name changed to reflect that the OpenDSS model is YYD
! Includes scripts for SLG fault cases

New Circuit.YYDXn BasekV=69  Bus1=B1 Isc3=1000000  Isc1=1100000  ! near-infinite source

Set Earthmodel=Carson

New Wiredata.ACSR336  GMR=0.0255000 DIAM=0.7410000 RAC=0.3060000     NormAmps=530.0000           Runits=mi radunits=in gmrunits=ft
New Wiredata.ACSR4/0  GMR=0.0081400 DIAM=0.5630000 RAC=0.5920000     NormAmps=340.0000           Runits=mi radunits=in gmrunits=ft

New LineGeometry.Line1-2 nconds=4 nphases=3
~ cond=1 wire=ACSR336 x=-6  h=50 units=ft
~ cond=2 wire=ACSR336 x=-8  h=42 units=ft
~ cond=3 wire=ACSR336 x=8   h=42 units=ft
~ cond=4 wire=ACSR4/0 x=0   h=58 units=ft
~ Reduce=Y

New LineGeometry.Line3-4 nconds=4 nphases=3
~ cond=1 wire=ACSR336 x=-3   h=28 units=ft
~ cond=2 wire=ACSR336 x=-1.5 h=28 units=ft
~ cond=3 wire=ACSR336 x=3    h=28 units=ft
~ cond=4 wire=ACSR4/0 x=0    h=24 units=ft
~ Reduce=Y

New LineGeometry.Line5-6 nconds=3 nphases=3
~ cond=1 wire=ACSR336 x=-7   h=28 units=ft
~ cond=2 wire=ACSR336 x=-4.5 h=28 units=ft
~ cond=3 wire=ACSR336 x=0    h=28 units=ft

// Lines
New Line.TLine1-2 Bus1=B1.1.2.3 Bus2=B2.1.2.3 Geometry=Line1-2 Length=10   units=mi
New Line.DLine3-4 Bus1=B3.1.2.3 Bus2=B4.1.2.3 Geometry=Line3-4 Length=1    units=mi
New Line.DLine5-6 Bus1=B5.1.2.3 Bus2=B6.1.2.3 Geometry=Line5-6 Length=150  units=ft

New Transformer.YYD Phases=3 Windings=3  XHL=7.5 XHT=36 XLT=28
~ wdg=1 bus=B2         conn=w  kv=69.0  kva=15000  %r=0.11
~ wdg=2 bus=B3.1.2.3.4 conn=w  kv=13.2  kva=15000  %r=0.11   Rneut=0 Xneut=0.4
~ wdg=3 bus=B5         conn=d  kv= 2.4  kva= 5000  %r=0.275

// Loads
New Load.B4a phases=1 Bus1=B4.1.0  kV=7.62 kW=1250 kvar=500 Model=1
New Load.B4b phases=1 Bus1=B4.2.0  kV=7.62 kW=1500 kvar=750 Model=1
New Load.B4c phases=1 Bus1=B4.3.0  kV=7.62 kW=1000 kvar=250 Model=1

New Load.B6 phases=3 Bus1=B6.1.2.3 Model=2 Conn=delta kV=2.4 kW=2250 kvar=1125

Set voltagebases=[69, 13.2, 2.4]
Calcvoltagebases
set tolerance=1e-8

solve

