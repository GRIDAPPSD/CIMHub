clear

new circuit.OYODBal   basekV=12.47 phases=3 mvasc3=200000 200000

set earthmodel=carson

new wiredata.conductor Runits=mi Rac=0.306 GMRunits=ft GMRac=0.0244  Radunits=in Diam=0.721 
new wiredata.neutral   Runits=mi Rac=0.592 GMRunits=ft GMRac=0.00814 Radunits=in Diam=0.563 
new linegeometry.4wire nconds=4 nphases=3 reduce=yes 
~ cond=1 wire=conductor units=ft x=-4   h=28 
~ cond=2 wire=conductor units=ft x=-1.5 h=28 
~ cond=3 wire=conductor units=ft x=3    h=28 
~ cond=4 wire=neutral   units=ft x=0    h=24 
new linegeometry.3wire nconds=3 nphases=3 reduce=no 
~ cond=1 wire=conductor units=ft x=-4   h=28 
~ cond=2 wire=conductor units=ft x=-1.5 h=28 
~ cond=3 wire=conductor units=ft x=3    h=28 

new line.line1 geometry=4wire length=2000 units=ft bus1=sourcebus bus2=n2

new transformer.t1 xhl=6 Phases=1 bank=Xfmr
~ wdg=1 bus=n2.1.0     conn=wye     kV=7.2   kVA=2000 %r=0.5  
~ wdg=2 bus=n3.1.2     conn=Delta   kV=4.16  kVA=2000 %r=0.5 
new transformer.t2 xhl=6 Phases=1 bank=Xfmr
~ wdg=1 bus=n2.2.0     conn=wye     kV=7.2   kVA=2000 %r=0.5 
~ wdg=2 bus=n3.2.3     conn=Delta   kV=4.16  kVA=2000 %r=0.5 

new line.line2 bus1=n3 bus2=n4 geometry=3wire length=2500 units=ft  

new load.load1 phases=3 bus1=n4 conn=Delta kV=4.16 kW=3600 pf=0.9  model=2 // 1
! Add 1-kvar Capacitor to stabilize the neutral point on the Delta Side
! (ppm=0 option would not propagate through the CIM)
New Capacitor.Stabilizer Bus1=N4  kV=4.16 kvar=1  Conn=wye  R=0

set voltagebases=[12.47, 4.16] 
calcvoltagebases  ! **** Delta-side bases are incorrect at low load
setkvbase n3 4.16
setkvbase n4 4.16
solve

