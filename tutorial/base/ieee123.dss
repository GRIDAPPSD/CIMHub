! Annotated Master file for the IEEE 123-bus test case.
!
! Note: DSS commands, property names, etc., are NOT case sensitive. Capitalize as you please.
! You should always do a "Clear" before making a new circuit:

Clear

! INSTANTIATE A NEW CIRCUIT AND DEFINE A STIFF 4160V SOURCE
! The new circuit is called "ieee123"
! This creates a Vsource object connected to "sourcebus". This is now the active circuit element, so
! you can simply continue to edit its property value.
! The basekV is redefined to 4.16 kV. The bus name is changed to "150" to match one of the buses in the test feeder.
! The source is set for 1.0 per unit and the Short circuit impedance is set to a small value (0.0001 ohms)
! The ~ is just shorthad for "more" for the New or Edit commands

New object=circuit.ieee123pv
~ basekv=4.16 Bus1=150 pu=1.00 R1=0 X1=0.0001 R0=0 X0=0.0001

set earthmodel=carson  // for compatibility with GridLAB-D and Kersting; the OpenDSS default is Deri

! 3-PHASE GANGED REGULATOR AT HEAD OF FEEDER (KERSTING ASSUMES NO IMPEDANCE IN THE REGULATOR)
! the first line defines the 3-phase transformer to be controlled by the regulator control.
! The 2nd line defines the properties of the regulator control according to the test case

new transformer.reg1a phases=3 windings=2 buses=[150 150r] conns=[wye wye] kvs=[4.16 4.16] kvas=[5000 5000] XHL=.001 %LoadLoss=0.00001 ppm=0.0
new regcontrol.creg1a transformer=reg1a winding=2 vreg=120 band=2 ptratio=20 ctprim=700 R=3 X=7.5

! These are only the line codes used in the 123-bus circuit

New linecode.1 nphases=3 BaseFreq=60 units=kft
~ rmatrix = [0.086666667 | 0.029545455 0.088371212 | 0.02907197 0.029924242 0.087405303]
~ xmatrix = [0.204166667 | 0.095018939 0.198522727 | 0.072897727 0.080227273 0.201723485]
~ cmatrix = [2.851710072 | -0.920293787  3.004631862 | -0.350755566  -0.585011253 2.71134756]
New linecode.2 nphases=3 BaseFreq=60 units=kft
~ rmatrix = [0.088371212 | 0.02992424  0.087405303 | 0.029545455 0.02907197 0.086666667]
~ xmatrix = [0.198522727 | 0.080227273  0.201723485 | 0.095018939 0.072897727 0.204166667]
~ cmatrix = [3.004631862 | -0.585011253 2.71134756 | -0.920293787  -0.350755566  2.851710072]
New linecode.3 nphases=3 BaseFreq=60 units=kft
~ rmatrix = [0.087405303 | 0.02907197 0.086666667  | 0.029924242 0.029545455 0.088371212]
~ xmatrix = [0.201723485 | 0.072897727 0.204166667 | 0.080227273 0.095018939 0.198522727]
~ cmatrix = [2.71134756  | -0.350755566 2.851710072 | -0.585011253 -0.920293787 3.004631862]
New linecode.4 nphases=3 BaseFreq=60 units=kft
~ rmatrix = [0.087405303 | 0.029924242 0.088371212 | 0.02907197   0.029545455 0.086666667]
~ xmatrix = [0.201723485 | 0.080227273 0.198522727 | 0.072897727 0.095018939 0.204166667]
~ cmatrix = [2.71134756  | -0.585011253 3.004631862 | -0.350755566 -0.920293787 2.851710072]
New linecode.5 nphases=3 BaseFreq=60 units=kft
~ rmatrix = [0.088371212  |  0.029545455  0.086666667  |  0.029924242  0.02907197  0.087405303]
~ xmatrix = [0.198522727  |  0.095018939  0.204166667  |  0.080227273  0.072897727  0.201723485]
~ cmatrix = [3.004631862  | -0.920293787  2.851710072  |  -0.585011253  -0.350755566  2.71134756]
New linecode.6 nphases=3 BaseFreq=60 units=kft
~ rmatrix = [0.086666667 | 0.02907197  0.087405303 | 0.029545455  0.029924242  0.088371212]
~ xmatrix = [0.204166667 | 0.072897727  0.201723485 | 0.095018939  0.080227273  0.198522727]
~ cmatrix = [2.851710072 | -0.350755566  2.71134756 | -0.920293787  -0.585011253  3.004631862]
New linecode.7 nphases=2 BaseFreq=60 units=kft
~ rmatrix = [0.086666667 | 0.02907197  0.087405303]
~ xmatrix = [0.204166667 | 0.072897727  0.201723485]
~ cmatrix = [2.569829596 | -0.52995137  2.597460011]
New linecode.8 nphases=2 BaseFreq=60 units=kft
~ rmatrix = [0.086666667 | 0.02907197  0.087405303]
~ xmatrix = [0.204166667 | 0.072897727  0.201723485]
~ cmatrix = [2.569829596 | -0.52995137  2.597460011]
New linecode.9 nphases=1 BaseFreq=60 units=kft
~ rmatrix = [0.251742424]
~ xmatrix = [0.255208333]
~ cmatrix = [2.270366128]
New linecode.10 nphases=1 BaseFreq=60 units=kft
~ rmatrix = [0.251742424]
~ xmatrix = [0.255208333]
~ cmatrix = [2.270366128]
New linecode.11 nphases=1 BaseFreq=60 units=kft
~ rmatrix = [0.251742424]
~ xmatrix = [0.255208333]
~ cmatrix = [2.270366128]
New linecode.12 nphases=3 BaseFreq=60 units=kft
~ rmatrix = [0.288049242 | 0.09844697  0.29032197 | 0.093257576  0.09844697  0.288049242]
~ xmatrix = [0.142443182 | 0.052556818  0.135643939 | 0.040852273  0.052556818  0.142443182]
~ cmatrix = [33.77150149 | 0  33.77150149 | 0  0  33.77150149]

! LINE DEFINITIONS
! Lines are defined by referring to a "linecode" that contains the impedances per unit length
! So the only properties required are the LineCode name and Length.  Units are assumed to match the definition
! since no units property is defined in either the Linecodes file or this file.
! Note that it is not necessary to explicitly specify the node connections for the 3-phase lines
! unless they are not ".1.2.3". However, they are spelled out here for clarity.
! The DSS assumes .1.2.3.0.0 ... for connections of 3 or more phases.
! Likewise, .1 is not necessary for 1-phase lines connected to phase 1. However, if it is connected
! to any other phase, it must be specified. For completeness, everything is spelled out here.
!
! Note that it is recommended that the "units=" property be used here and in the Linecode definition as well
! to avoid confusion in the future

! *** Original *** New Line.L115   Phases=3 Bus1=149.1.2.3  Bus2=1.1.2.3    LineCode=1    Length=0.4
! Since the default is 3-phase, the definition of this line can be simpler:

New Line.L115            Bus1=149        Bus2=1          LineCode=1    Length=0.4    units=kft

New Line.L1     Phases=1 Bus1=1.2        Bus2=2.2        LineCode=10   Length=0.175  units=kft
New Line.L2     Phases=1 Bus1=1.3        Bus2=3.3        LineCode=11   Length=0.25   units=kft
New Line.L3     Phases=3 Bus1=1.1.2.3    Bus2=7.1.2.3    LineCode=1    Length=0.3    units=kft
New Line.L4     Phases=1 Bus1=3.3        Bus2=4.3        LineCode=11   Length=0.2    units=kft
New Line.L5     Phases=1 Bus1=3.3        Bus2=5.3        LineCode=11   Length=0.325  units=kft
New Line.L6     Phases=1 Bus1=5.3        Bus2=6.3        LineCode=11   Length=0.25   units=kft
New Line.L7     Phases=3 Bus1=7.1.2.3    Bus2=8.1.2.3    LineCode=1    Length=0.2    units=kft
New Line.L8     Phases=1 Bus1=8.2        Bus2=12.2       LineCode=10   Length=0.225  units=kft
New Line.L9     Phases=1 Bus1=8.1        Bus2=9.1        LineCode=9    Length=0.225  units=kft
New Line.L10    Phases=3 Bus1=8.1.2.3    Bus2=13.1.2.3   LineCode=1    Length=0.3    units=kft
New Line.L11    Phases=1 Bus1=9r.1       Bus2=14.1       LineCode=9    Length=0.425  units=kft
New Line.L12    Phases=1 Bus1=13.3       Bus2=34.3       LineCode=11   Length=0.15   units=kft
New Line.L13    Phases=3 Bus1=13.1.2.3   Bus2=18.1.2.3   LineCode=2    Length=0.825  units=kft
New Line.L14    Phases=1 Bus1=14.1       Bus2=11.1       LineCode=9    Length=0.25   units=kft
New Line.L15    Phases=1 Bus1=14.1       Bus2=10.1       LineCode=9    Length=0.25   units=kft
New Line.L16    Phases=1 Bus1=15.3       Bus2=16.3       LineCode=11   Length=0.375  units=kft
New Line.L17    Phases=1 Bus1=15.3       Bus2=17.3       LineCode=11   Length=0.35   units=kft
New Line.L18    Phases=1 Bus1=18.1       Bus2=19.1       LineCode=9    Length=0.25   units=kft
New Line.L19    Phases=3 Bus1=18.1.2.3   Bus2=21.1.2.3   LineCode=2    Length=0.3    units=kft
New Line.L20    Phases=1 Bus1=19.1       Bus2=20.1       LineCode=9    Length=0.325  units=kft
New Line.L21    Phases=1 Bus1=21.2       Bus2=22.2       LineCode=10   Length=0.525  units=kft
New Line.L22    Phases=3 Bus1=21.1.2.3   Bus2=23.1.2.3   LineCode=2    Length=0.25   units=kft
New Line.L23    Phases=1 Bus1=23.3       Bus2=24.3       LineCode=11   Length=0.55   units=kft
New Line.L24    Phases=3 Bus1=23.1.2.3   Bus2=25.1.2.3   LineCode=2    Length=0.275  units=kft
New Line.L25    Phases=2 Bus1=25r.1.3    Bus2=26.1.3     LineCode=7    Length=0.35   units=kft
New Line.L26    Phases=3 Bus1=25.1.2.3   Bus2=28.1.2.3   LineCode=2    Length=0.2    units=kft
New Line.L27    Phases=2 Bus1=26.1.3     Bus2=27.1.3     LineCode=7    Length=0.275  units=kft
New Line.L28    Phases=1 Bus1=26.3       Bus2=31.3       LineCode=11   Length=0.225  units=kft
New Line.L29    Phases=1 Bus1=27.1       Bus2=33.1       LineCode=9    Length=0.5    units=kft
New Line.L30    Phases=3 Bus1=28.1.2.3   Bus2=29.1.2.3   LineCode=2    Length=0.3    units=kft
New Line.L31    Phases=3 Bus1=29.1.2.3   Bus2=30.1.2.3   LineCode=2    Length=0.35   units=kft
New Line.L32    Phases=3 Bus1=30.1.2.3   Bus2=250.1.2.3  LineCode=2    Length=0.2    units=kft
New Line.L33    Phases=1 Bus1=31.3       Bus2=32.3       LineCode=11   Length=0.3    units=kft
New Line.L34    Phases=1 Bus1=34.3       Bus2=15.3       LineCode=11   Length=0.1    units=kft
New Line.L35    Phases=2 Bus1=35.1.2     Bus2=36.1.2     LineCode=8    Length=0.65   units=kft
New Line.L36    Phases=3 Bus1=35.1.2.3   Bus2=40.1.2.3   LineCode=1    Length=0.25   units=kft
New Line.L37    Phases=1 Bus1=36.1       Bus2=37.1       LineCode=9    Length=0.3    units=kft
New Line.L38    Phases=1 Bus1=36.2       Bus2=38.2       LineCode=10   Length=0.25   units=kft
New Line.L39    Phases=1 Bus1=38.2       Bus2=39.2       LineCode=10   Length=0.325  units=kft
New Line.L40    Phases=1 Bus1=40.3       Bus2=41.3       LineCode=11   Length=0.325  units=kft
New Line.L41    Phases=3 Bus1=40.1.2.3   Bus2=42.1.2.3   LineCode=1    Length=0.25   units=kft
New Line.L42    Phases=1 Bus1=42.2       Bus2=43.2       LineCode=10   Length=0.5    units=kft
New Line.L43    Phases=3 Bus1=42.1.2.3   Bus2=44.1.2.3   LineCode=1    Length=0.2    units=kft
New Line.L44    Phases=1 Bus1=44.1       Bus2=45.1       LineCode=9    Length=0.2    units=kft
New Line.L45    Phases=3 Bus1=44.1.2.3   Bus2=47.1.2.3   LineCode=1    Length=0.25   units=kft
New Line.L46    Phases=1 Bus1=45.1       Bus2=46.1       LineCode=9    Length=0.3    units=kft
New Line.L47    Phases=3 Bus1=47.1.2.3   Bus2=48.1.2.3   LineCode=4    Length=0.15   units=kft
New Line.L48    Phases=3 Bus1=47.1.2.3   Bus2=49.1.2.3   LineCode=4    Length=0.25   units=kft
New Line.L49    Phases=3 Bus1=49.1.2.3   Bus2=50.1.2.3   LineCode=4    Length=0.25   units=kft
New Line.L50    Phases=3 Bus1=50.1.2.3   Bus2=51.1.2.3   LineCode=4    Length=0.25   units=kft
New Line.L51    Phases=3 Bus1=51.1.2.3   Bus2=151.1.2.3  LineCode=4    Length=0.5    units=kft
New Line.L52    Phases=3 Bus1=52.1.2.3   Bus2=53.1.2.3   LineCode=1    Length=0.2    units=kft
New Line.L53    Phases=3 Bus1=53.1.2.3   Bus2=54.1.2.3   LineCode=1    Length=0.125  units=kft
New Line.L54    Phases=3 Bus1=54.1.2.3   Bus2=55.1.2.3   LineCode=1    Length=0.275  units=kft
New Line.L55    Phases=3 Bus1=54.1.2.3   Bus2=57.1.2.3   LineCode=3    Length=0.35   units=kft
New Line.L56    Phases=3 Bus1=55.1.2.3   Bus2=56.1.2.3   LineCode=1    Length=0.275  units=kft
New Line.L57    Phases=1 Bus1=57.2       Bus2=58.2       LineCode=10   Length=0.25   units=kft
New Line.L58    Phases=3 Bus1=57.1.2.3   Bus2=60.1.2.3   LineCode=3    Length=0.75   units=kft
New Line.L59    Phases=1 Bus1=58.2       Bus2=59.2       LineCode=10   Length=0.25   units=kft
New Line.L60    Phases=3 Bus1=60.1.2.3   Bus2=61.1.2.3   LineCode=5    Length=0.55   units=kft
New Line.L61    Phases=3 Bus1=60.1.2.3   Bus2=62.1.2.3   LineCode=12   Length=0.25   units=kft
New Line.L62    Phases=3 Bus1=62.1.2.3   Bus2=63.1.2.3   LineCode=12   Length=0.175  units=kft
New Line.L63    Phases=3 Bus1=63.1.2.3   Bus2=64.1.2.3   LineCode=12   Length=0.35   units=kft
New Line.L64    Phases=3 Bus1=64.1.2.3   Bus2=65.1.2.3   LineCode=12   Length=0.425  units=kft
New Line.L65    Phases=3 Bus1=65.1.2.3   Bus2=66.1.2.3   LineCode=12   Length=0.325  units=kft
New Line.L66    Phases=1 Bus1=67.1       Bus2=68.1       LineCode=9    Length=0.2    units=kft
New Line.L67    Phases=3 Bus1=67.1.2.3   Bus2=72.1.2.3   LineCode=3    Length=0.275  units=kft
New Line.L68    Phases=3 Bus1=67.1.2.3   Bus2=97.1.2.3   LineCode=3    Length=0.25   units=kft
New Line.L69    Phases=1 Bus1=68.1       Bus2=69.1       LineCode=9    Length=0.275  units=kft
New Line.L70    Phases=1 Bus1=69.1       Bus2=70.1       LineCode=9    Length=0.325  units=kft
New Line.L71    Phases=1 Bus1=70.1       Bus2=71.1       LineCode=9    Length=0.275  units=kft
New Line.L72    Phases=1 Bus1=72.3       Bus2=73.3       LineCode=11   Length=0.275  units=kft
New Line.L73    Phases=3 Bus1=72.1.2.3   Bus2=76.1.2.3   LineCode=3    Length=0.2    units=kft
New Line.L74    Phases=1 Bus1=73.3       Bus2=74.3       LineCode=11   Length=0.35   units=kft
New Line.L75    Phases=1 Bus1=74.3       Bus2=75.3       LineCode=11   Length=0.4    units=kft
New Line.L76    Phases=3 Bus1=76.1.2.3   Bus2=77.1.2.3   LineCode=6    Length=0.4    units=kft
New Line.L77    Phases=3 Bus1=76.1.2.3   Bus2=86.1.2.3   LineCode=3    Length=0.7    units=kft
New Line.L78    Phases=3 Bus1=77.1.2.3   Bus2=78.1.2.3   LineCode=6    Length=0.1    units=kft
New Line.L79    Phases=3 Bus1=78.1.2.3   Bus2=79.1.2.3   LineCode=6    Length=0.225  units=kft
New Line.L80    Phases=3 Bus1=78.1.2.3   Bus2=80.1.2.3   LineCode=6    Length=0.475  units=kft
New Line.L81    Phases=3 Bus1=80.1.2.3   Bus2=81.1.2.3   LineCode=6    Length=0.175  units=kft
New Line.L82    Phases=3 Bus1=81.1.2.3   Bus2=82.1.2.3   LineCode=6    Length=0.25   units=kft
New Line.L83    Phases=1 Bus1=81.3       Bus2=84.3       LineCode=11   Length=0.675  units=kft
New Line.L84    Phases=3 Bus1=82.1.2.3   Bus2=83.1.2.3   LineCode=6    Length=0.25   units=kft
New Line.L85    Phases=1 Bus1=84.3       Bus2=85.3       LineCode=11   Length=0.475  units=kft
New Line.L86    Phases=3 Bus1=86.1.2.3   Bus2=87.1.2.3   LineCode=6    Length=0.45   units=kft
New Line.L87    Phases=1 Bus1=87.1       Bus2=88.1       LineCode=9    Length=0.175  units=kft
New Line.L88    Phases=3 Bus1=87.1.2.3   Bus2=89.1.2.3   LineCode=6    Length=0.275  units=kft
New Line.L89    Phases=1 Bus1=89.2       Bus2=90.2       LineCode=10   Length=0.25   units=kft
New Line.L90    Phases=3 Bus1=89.1.2.3   Bus2=91.1.2.3   LineCode=6    Length=0.225  units=kft
New Line.L91    Phases=1 Bus1=91.3       Bus2=92.3       LineCode=11   Length=0.3    units=kft
New Line.L92    Phases=3 Bus1=91.1.2.3   Bus2=93.1.2.3   LineCode=6    Length=0.225  units=kft
New Line.L93    Phases=1 Bus1=93.1       Bus2=94.1       LineCode=9    Length=0.275  units=kft
New Line.L94    Phases=3 Bus1=93.1.2.3   Bus2=95.1.2.3   LineCode=6    Length=0.3    units=kft
New Line.L95    Phases=1 Bus1=95.2       Bus2=96.2       LineCode=10   Length=0.2    units=kft
New Line.L96    Phases=3 Bus1=97.1.2.3   Bus2=98.1.2.3   LineCode=3    Length=0.275  units=kft
New Line.L97    Phases=3 Bus1=98.1.2.3   Bus2=99.1.2.3   LineCode=3    Length=0.55   units=kft
New Line.L98    Phases=3 Bus1=99.1.2.3   Bus2=100.1.2.3  LineCode=3    Length=0.3    units=kft
New Line.L99    Phases=3 Bus1=100.1.2.3  Bus2=450.1.2.3  LineCode=3    Length=0.8    units=kft
New Line.L118   Phases=3 Bus1=197.1.2.3  Bus2=101.1.2.3  LineCode=3    Length=0.25   units=kft
New Line.L100   Phases=1 Bus1=101.3      Bus2=102.3      LineCode=11   Length=0.225  units=kft
New Line.L101   Phases=3 Bus1=101.1.2.3  Bus2=105.1.2.3  LineCode=3    Length=0.275  units=kft
New Line.L102   Phases=1 Bus1=102.3      Bus2=103.3      LineCode=11   Length=0.325  units=kft
New Line.L103   Phases=1 Bus1=103.3      Bus2=104.3      LineCode=11   Length=0.7    units=kft
New Line.L104   Phases=1 Bus1=105.2      Bus2=106.2      LineCode=10   Length=0.225  units=kft
New Line.L105   Phases=3 Bus1=105.1.2.3  Bus2=108.1.2.3  LineCode=3    Length=0.325  units=kft
New Line.L106   Phases=1 Bus1=106.2      Bus2=107.2      LineCode=10   Length=0.575  units=kft
New Line.L107   Phases=1 Bus1=108.1      Bus2=109.1      LineCode=9    Length=0.45   units=kft
New Line.L108   Phases=3 Bus1=108.1.2.3  Bus2=300.1.2.3  LineCode=3    Length=1      units=kft
New Line.L109   Phases=1 Bus1=109.1      Bus2=110.1      LineCode=9    Length=0.3    units=kft
New Line.L110   Phases=1 Bus1=110.1      Bus2=111.1      LineCode=9    Length=0.575  units=kft
New Line.L111   Phases=1 Bus1=110.1      Bus2=112.1      LineCode=9    Length=0.125  units=kft
New Line.L112   Phases=1 Bus1=112.1      Bus2=113.1      LineCode=9    Length=0.525  units=kft
New Line.L113   Phases=1 Bus1=113.1      Bus2=114.1      LineCode=9    Length=0.325  units=kft
New Line.L114   Phases=3 Bus1=135.1.2.3  Bus2=35.1.2.3   LineCode=4    Length=0.375  units=kft
New Line.L116   Phases=3 Bus1=152.1.2.3  Bus2=52.1.2.3   LineCode=1    Length=0.4    units=kft
New Line.L117   Phases=3 Bus1=160r.1.2.3 Bus2=67.1.2.3   LineCode=6    Length=0.35   units=kft


! NORMALLY CLOSED SWITCHES ARE DEFINED AS SHORT LINES
! Could also be defned by setting the Switch=Yes property

New Line.Sw1    phases=3  Bus1=150r   Bus2=149        Switch=y    r1=1e-3 r0=1e-3 x1=0.000 x0=0.000 c1=0.000 c0=0.000 Length=0.001 units=kft
New Line.Sw2    phases=3  Bus1=13     Bus2=152        switch=y    r1=1e-3 r0=1e-3 x1=0.000 x0=0.000 c1=0.000 c0=0.000 Length=0.001 units=kft
New Line.Sw3    phases=3  Bus1=18     Bus2=135        switch=y    r1=1e-3 r0=1e-3 x1=0.000 x0=0.000 c1=0.000 c0=0.000 Length=0.001 units=kft
New Line.Sw4    phases=3  Bus1=60     Bus2=160        switch=y    r1=1e-3 r0=1e-3 x1=0.000 x0=0.000 c1=0.000 c0=0.000 Length=0.001 units=kft
New Line.Sw5    phases=3  Bus1=97     Bus2=197        switch=y    r1=1e-3 r0=1e-3 x1=0.000 x0=0.000 c1=0.000 c0=0.000 Length=0.001 units=kft
New Line.Sw6    phases=3  Bus1=61     Bus2=61s        switch=y    r1=1e-3 r0=1e-3 x1=0.000 x0=0.000 c1=0.000 c0=0.000 Length=0.001 units=kft

! NORMALLY OPEN SWITCHES; DEFINED AS SHORT LINE TO OPEN BUS SO WE CAN SEE OPEN POINT VOLTAGES.
! COULD ALSO BE DEFINED AS DISABLED OR THE TERMINCAL COULD BE OPENED AFTER BEING DEFINED

New Line.Sw7    phases=3  Bus1=151    Bus2=300_OPEN       switch=y    r1=1e-3 r0=1e-3  x1=0.000 x0=0.000 c1=0.000 c0=0.000 Length=0.001 units=kft
New Line.Sw8    phases=1  Bus1=54.1   Bus2=94_OPEN.1      switch=y    r1=1e-3 r0=1e-3  x1=0.000 x0=0.000 c1=0.000 c0=0.000 Length=0.001 units=kft

! LOAD TRANSFORMER AT 61s/610
! This is a 150 kVA Delta-Delta stepdown from 4160V to 480V.

New Transformer.XFM1  Phases=3   Windings=2 Xhl=2.72
~ wdg=1 bus=61s       conn=Delta kv=4.16    kva=150    %r=0.635
~ wdg=2 bus=610       conn=Delta kv=0.48    kva=150    %r=0.635

! CAPACITORS
! Capacitors are 2-terminal devices. The 2nd terminal (Bus2=...) defaults to all phases
! connected to ground (Node 0). Thus, it need not be specified if a Y-connected or L-N connected
! capacitor is desired

New Capacitor.C83       Bus1=83      Phases=3     kVAR=600     kV=4.16
New Capacitor.C88a      Bus1=88.1    Phases=1     kVAR=50      kV=2.402
New Capacitor.C90b      Bus1=90.2    Phases=1     kVAR=50      kV=2.402
New Capacitor.C92c      Bus1=92.3    Phases=1     kVAR=50      kV=2.402

!REGULATORS - REDIRECT TO DEFINITIONS FILE
! This file contains definitions for the remainder of regulators on the feeder:

!DEFINE TRANSFORMERS FOR REGULATORS
! Have to assume basically zero impedance regulators to match the test case
new transformer.reg2a phases=1    windings=2 bank=reg2  buses=[9.1     9r.1] conns=[wye wye] kvs=[2.402 2.402] kvas=[2000 2000] XHL=.01 %LoadLoss=0.00001 ppm=0.0 
new transformer.reg3a phases=1    windings=2 bank=reg3  buses=[25.1   25r.1] conns=[wye wye] kvs=[2.402 2.402] kvas=[2000 2000] XHL=.01 %LoadLoss=0.00001 ppm=0.0 
new transformer.reg4a phases=1    windings=2 bank=reg4  buses=[160.1 160r.1] conns=[wye wye] kvs=[2.402 2.402] kvas=[2000 2000] XHL=.01 %LoadLoss=0.00001 ppm=0.0 
new transformer.reg3c like=reg3a             bank=reg3  buses=[25.3   25r.3] ppm=0.0 
new transformer.reg4b like=reg4a             bank=reg4  buses=[160.2 160r.2] ppm=0.0 
new transformer.reg4c like=reg4a             bank=reg4  buses=[160.3 160r.3] ppm=0.0 

! POINT REGULATOR CONTROLS TO REGULATOR TRANSFORMER AND SET PARAMETERS
new regcontrol.creg2a             transformer=reg2a winding=2 vreg=120 band=2 ptratio=20 ctprim=50  R=0.4   X=0.4  
new regcontrol.creg3a             transformer=reg3a winding=2 vreg=120 band=1 ptratio=20 ctprim=50  R=0.4   X=0.4  
new regcontrol.creg3c like=creg3a transformer=reg3c 
new regcontrol.creg4a             transformer=reg4a winding=2 vreg=124 band=2 ptratio=20 ctprim=300 R=0.6   X=1.3  
new regcontrol.creg4b like=creg4a transformer=reg4b                                                 R=1.4   X=2.6   
new regcontrol.creg4c like=creg4a transformer=reg4c                                                 R=0.2   X=1.4   

! NOTE: WHEN LIKE= IS USED, IT IS NECESSARY TO SPECIFY ONLY THOSE PROPERTIES THAT ARE DIFFERENT

!
! LOAD DEFINITIONS
!
! These are three-phase, or 1-phase delta loads, assumed to be commercial.
! They will not be converted to secondary service.

New Load.S35a  Bus1=35.1.2 Phases=1 Conn=Delta Model=1 kV=4.160 kW=40.0  kvar=20.0  
New Load.S47   Bus1=47     Phases=3 Conn=Wye   Model=5 kV=4.160 kW=105.0 kvar=75.0  
New Load.S48   Bus1=48     Phases=3 Conn=Wye   Model=2 kV=4.160 kW=210.0 kVAR=150.0 
New Load.S65a  Bus1=65.1.2 Phases=1 Conn=Delta Model=2 kV=4.160 kW=35.0  kvar=25.0  
New Load.S65b  Bus1=65.2.3 Phases=1 Conn=Delta Model=2 kV=4.160 kW=35.0  kvar=25.0  
New Load.S65c  Bus1=65.3.1 Phases=1 Conn=Delta Model=2 kV=4.160 kW=70.0  kvar=50.0  
New Load.S76a  Bus1=76.1.2 Phases=1 Conn=Delta Model=5 kV=4.160 kW=105.0 kvar=80.0  
New Load.S76b  Bus1=76.2.3 Phases=1 Conn=Delta Model=5 kV=4.160 kW=70.0  kvar=50.0  
New Load.S76c  Bus1=76.3.1 Phases=1 Conn=Delta Model=5 kV=4.160 kW=70.0  kvar=50.0    
!
! These one-phase wye loads have been converted to secondary service, balanced s1 and s2.
! Definitions of the 1-phase "center tapped" 120/240V service transformers.
! These transformers are defined as 3-winding transformers (as they are in reality)
! The primary winding is connected 2400V line-to-neutral on one of the phases.
! The secondary windings are consistently connected 1.0 and 0.2 respectively to get the polarity correct.
! 
New XfmrCode.CT25  phases=1 windings=3 kvs=[2.4 0.12 0.12] kVAs=[ 25.0  25.0  25.0] %imag=0.5 %Rs=[0.6 1.2 1.2] %noloadloss=.2 Xhl=2.04  Xht=2.04  Xlt=1.36
New XfmrCode.CT50  phases=1 windings=3 kvs=[2.4 0.12 0.12] kVAs=[ 50.0  50.0  50.0] %imag=0.5 %Rs=[0.6 1.2 1.2] %noloadloss=.2 Xhl=2.04  Xht=2.04  Xlt=1.36
New XfmrCode.CT100 phases=1 windings=3 kvs=[2.4 0.12 0.12] kVAs=[100.0 100.0 100.0] %imag=0.5 %Rs=[0.6 1.2 1.2] %noloadloss=.2 Xhl=2.04  Xht=2.04  Xlt=1.36
!
!  Loads are defined as two 120V loads connected line to neutral
!  For phases>1, OpenDSS Load model assumes that a 2-phase load is 2 phases of a 3-phase system 
!  and requires the voltage base to be specified same as 3-phase (Line-Line kV = Line-Neutral * sqrt(3))
!  Thus, the base voltage is 208V to get 120V line-to-neutral.
!  Alternatively, we could have defined two separate 1-phase loads rated at 120V.
!  The service transformer connection enforces the voltage polarities and phase angles.
!  The kW load is divided equally between the two "phases"
!  Replace each original primary load with a transformer plus triplex load like this...
//New Load.S1a   Bus1=1.1    Phases=1 Conn=Wye   Model=1 kV=2.4   kW=40.0  kvar=20.0
New Transformer.XFS1a  XfmrCode=CT50 buses=[1.1 s1a.1.0 s1a.0.2]
New Load.S1a          Bus1=s1a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0  
  
New Transformer.XFS2b  XfmrCode=CT25 buses=[2.2 s2b.1.0 s2b.0.2]
New Load.S2b         Bus1=s2b.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS4c  XfmrCode=CT50 buses=[4.3 s4c.1.0 s4c.0.2]
New Load.S4c          Bus1=s4c.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS5c  XfmrCode=CT25 buses=[5.3 s5c.1.0 s5c.0.2]
New Load.S5c          Bus1=s5c.1.2   Phases=2 Conn=Wye   Model=5 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS6c  XfmrCode=CT50 buses=[6.3 s6c.1.0 s6c.0.2]
New Load.S6c          Bus1=s6c.1.2   Phases=2 Conn=Wye   Model=2 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS7a  XfmrCode=CT25 buses=[7.1 s7a.1.0 s7a.0.2]
New Load.S7a          Bus1=s7a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS9a  XfmrCode=CT50 buses=[9.1 s9a.1.0 s9a.0.2]
New Load.S9a          Bus1=s9a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS10a  XfmrCode=CT25 buses=[10.1 s10a.1.0 s10a.0.2]
New Load.S10a          Bus1=s10a.1.2   Phases=2 Conn=Wye   Model=5 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS11a  XfmrCode=CT50 buses=[11.1 s11a.1.0 s11a.0.2]
New Load.S11a          Bus1=s11a.1.2   Phases=2 Conn=Wye   Model=2 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS12b  XfmrCode=CT25 buses=[12.2 s12b.1.0 s12b.0.2]
New Load.S12b          Bus1=s12b.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS16c  XfmrCode=CT50 buses=[16.3 s16c.1.0 s16c.0.2]
New Load.S16c          Bus1=s16c.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS17c  XfmrCode=CT25 buses=[17.3 s17c.1.0 s17c.0.2]
New Load.S17c          Bus1=s17c.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS19a  XfmrCode=CT50 buses=[19.1 s19a.1.0 s19a.0.2]
New Load.S19a          Bus1=s19a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS20a  XfmrCode=CT50 buses=[20.1 s20a.1.0 s20a.0.2]
New Load.S20a          Bus1=s20a.1.2   Phases=2 Conn=Wye   Model=5 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS22b  XfmrCode=CT50 buses=[22.2 s22b.1.0 s22b.0.2]
New Load.S22b          Bus1=s22b.1.2   Phases=2 Conn=Wye   Model=2 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS24c  XfmrCode=CT50 buses=[24.3 s24c.1.0 s24c.0.2]
New Load.S24c          Bus1=s24c.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS28a  XfmrCode=CT50 buses=[28.1 s28a.1.0 s28a.0.2]
New Load.S28a          Bus1=s28a.1.2   Phases=2 Conn=Wye   Model=5 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS29a  XfmrCode=CT50 buses=[29.1 s29a.1.0 s29a.0.2]
New Load.S29a          Bus1=s29a.1.2   Phases=2 Conn=Wye   Model=2 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS30c  XfmrCode=CT50 buses=[30.3 s30c.1.0 s30c.0.2]
New Load.S30c          Bus1=s30c.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS31c  XfmrCode=CT25 buses=[31.3 s31c.1.0 s31c.0.2]
New Load.S31c          Bus1=s31c.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS32c  XfmrCode=CT25 buses=[32.3 s32c.1.0 s32c.0.2]
New Load.S32c          Bus1=s32c.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS33a  XfmrCode=CT50 buses=[33.1 s33a.1.0 s33a.0.2]
New Load.S33a          Bus1=s33a.1.2   Phases=2 Conn=Wye   Model=5 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS34c  XfmrCode=CT50 buses=[34.3 s34c.1.0 s34c.0.2]
New Load.S34c          Bus1=s34c.1.2   Phases=2 Conn=Wye   Model=2 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS37a  XfmrCode=CT50 buses=[37.1 s37a.1.0 s37a.0.2]
New Load.S37a         Bus1=s37a.1.2   Phases=2 Conn=Wye   Model=2 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS38b  XfmrCode=CT25 buses=[38.2 s38b.1.0 s38b.0.2]
New Load.S38b          Bus1=s38b.1.2   Phases=2 Conn=Wye   Model=5 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS39b  XfmrCode=CT25 buses=[39.2 s39b.1.0 s39b.0.2]
New Load.S39b          Bus1=s39b.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS41c  XfmrCode=CT25 buses=[41.3 s41c.1.0 s41c.0.2]
New Load.S41c          Bus1=s41c.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS42a  XfmrCode=CT25 buses=[42.1 s42a.1.0 s42a.0.2]
New Load.S42a          Bus1=s42a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS43b  XfmrCode=CT50 buses=[43.2 s43b.1.0 s43b.0.2]
New Load.S43b         Bus1=s43b.1.2   Phases=2 Conn=Wye   Model=2 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS45a  XfmrCode=CT25 buses=[45.1 s45a.1.0 s45a.0.2]
New Load.S45a          Bus1=s45a.1.2   Phases=2 Conn=Wye   Model=5 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS46a  XfmrCode=CT25 buses=[46.1 s46a.1.0 s46a.0.2]
New Load.S46a          Bus1=s46a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS49a  XfmrCode=CT50 buses=[49.1 s49a.1.0 s49a.0.2]
New Load.S49a          Bus1=s49a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=35.0  kvar=25.0
New Transformer.XFS49b  XfmrCode=CT100 buses=[49.2 s49b.1.0 s49b.0.2]
New Load.S49b          Bus1=s49b.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=70.0  kvar=50.0
New Transformer.XFS49c  XfmrCode=CT50 buses=[49.3 s49c.1.0 s49c.0.2]
New Load.S49c          Bus1=s49c.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=35.0  kvar=20.0
New Transformer.XFS50c  XfmrCode=CT50 buses=[50.3 s50c.1.0 s50c.0.2]
New Load.S50c         Bus1=s50c.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS51a  XfmrCode=CT25 buses=[51.1 s51a.1.0 s51a.0.2]
New Load.S51a          Bus1=s51a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS52a  XfmrCode=CT50 buses=[52.1 s52a.1.0 s52a.0.2]
New Load.S52a         Bus1=s52a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS53a  XfmrCode=CT50 buses=[53.1 s53a.1.0 s53a.0.2]
New Load.S53a         Bus1=s53a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS55a  XfmrCode=CT25 buses=[55.1 s55a.1.0 s55a.0.2]
New Load.S55a         Bus1=s55a.1.2   Phases=2 Conn=Wye   Model=2 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS56b  XfmrCode=CT25 buses=[56.2 s56b.1.0 s56b.0.2]
New Load.S56b         Bus1=s56b.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS58b  XfmrCode=CT25 buses=[58.2 s58b.1.0 s58b.0.2]
New Load.S58b         Bus1=s58b.1.2   Phases=2 Conn=Wye   Model=5 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS59b  XfmrCode=CT25 buses=[59.2 s59b.1.0 s59b.0.2]
New Load.S59b         Bus1=s59b.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS60a  XfmrCode=CT25 buses=[60.1 s60a.1.0 s60a.0.2]
New Load.S60a         Bus1=s60a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS62c  XfmrCode=CT50 buses=[62.3 s62c.1.0 s62c.0.2]
New Load.S62c         Bus1=s62c.1.2   Phases=2 Conn=Wye   Model=2 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS63a  XfmrCode=CT50 buses=[63.1 s63a.1.0 s63a.0.2]
New Load.S63a         Bus1=s63a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS64b  XfmrCode=CT100 buses=[64.2 s64b.1.0 s64b.0.2]
New Load.S64b          Bus1=s64b.1.2   Phases=2 Conn=Wye   Model=5 kV=0.208   kW=75.0  kvar=35.0
New Transformer.XFS66c  XfmrCode=CT100 buses=[66.3 s66c.1.0 s66c.0.2]
New Load.S66c          Bus1=s66c.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=75.0  kvar=35.0
New Transformer.XFS68a  XfmrCode=CT25 buses=[68.1 s68a.1.0 s68a.0.2]
New Load.S68a         Bus1=s68a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS69a  XfmrCode=CT50 buses=[69.1 s69a.1.0 s69a.0.2]
New Load.S69a         Bus1=s69a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS70a  XfmrCode=CT25 buses=[70.1 s70a.1.0 s70a.0.2]
New Load.S70a         Bus1=s70a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS71a  XfmrCode=CT50 buses=[71.1 s71a.1.0 s71a.0.2]
New Load.S71a         Bus1=s71a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS73c  XfmrCode=CT50 buses=[73.3 s73c.1.0 s73c.0.2]
New Load.S73c         Bus1=s73c.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS74c  XfmrCode=CT50 buses=[74.3 s74c.1.0 s74c.0.2]
New Load.S74c         Bus1=s74c.1.2   Phases=2 Conn=Wye   Model=2 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS75c  XfmrCode=CT50 buses=[75.3 s75c.1.0 s75c.0.2]
New Load.S75c         Bus1=s75c.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS77b  XfmrCode=CT50 buses=[77.2 s77b.1.0 s77b.0.2]
New Load.S77b         Bus1=s77b.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS79a  XfmrCode=CT50 buses=[79.1 s79a.1.0 s79a.0.2]
New Load.S79a         Bus1=s79a.1.2   Phases=2 Conn=Wye   Model=2 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS80b  XfmrCode=CT50 buses=[80.2 s80b.1.0 s80b.0.2]
New Load.S80b         Bus1=s80b.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS82a  XfmrCode=CT50 buses=[82.1 s82a.1.0 s82a.0.2]
New Load.S82a         Bus1=s82a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS83c  XfmrCode=CT25 buses=[83.3 s83c.1.0 s83c.0.2]
New Load.S83c         Bus1=s83c.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS84c  XfmrCode=CT25 buses=[84.3 s84c.1.0 s84c.0.2]
New Load.S84c         Bus1=s84c.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS85c  XfmrCode=CT50 buses=[85.3 s85c.1.0 s85c.0.2]
New Load.S85c         Bus1=s85c.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS86b  XfmrCode=CT25 buses=[86.2 s86b.1.0 s86b.0.2]
New Load.S86b         Bus1=s86b.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS87b  XfmrCode=CT50 buses=[87.2 s87b.1.0 s87b.0.2]
New Load.S87b         Bus1=s87b.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS88a  XfmrCode=CT50 buses=[88.1 s88a.1.0 s88a.0.2]
New Load.S88a         Bus1=s88a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS90b  XfmrCode=CT50 buses=[90.2 s90b.1.0 s90b.0.2]
New Load.S90b         Bus1=s90b.1.2   Phases=2 Conn=Wye   Model=5 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS92c  XfmrCode=CT50 buses=[92.3 s92c.1.0 s92c.0.2]
New Load.S92c         Bus1=s92c.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS94a  XfmrCode=CT50 buses=[94.1 s94a.1.0 s94a.0.2]
New Load.S94a         Bus1=s94a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS95b  XfmrCode=CT25 buses=[95.2 s95b.1.0 s95b.0.2]
New Load.S95b         Bus1=s95b.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS96b  XfmrCode=CT25 buses=[96.2 s96b.1.0 s96b.0.2]
New Load.S96b         Bus1=s96b.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS98a  XfmrCode=CT50 buses=[98.1 s98a.1.0 s98a.0.2]
New Load.S98a         Bus1=s98a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS99b  XfmrCode=CT50 buses=[99.2 s99b.1.0 s99b.0.2]
New Load.S99b         Bus1=s99b.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS100c  XfmrCode=CT50 buses=[100.3 s100c.1.0 s100c.0.2]
New Load.S100c         Bus1=s100c.1.2   Phases=2 Conn=Wye   Model=2 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS102c  XfmrCode=CT25 buses=[102.3 s102c.1.0 s102c.0.2]
New Load.S102c         Bus1=s102c.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS103c  XfmrCode=CT50 buses=[103.3 s103c.1.0 s103c.0.2]
New Load.S103c         Bus1=s103c.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS104c  XfmrCode=CT50 buses=[104.3 s104c.1.0 s104c.0.2]
New Load.S104c         Bus1=s104c.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS106b  XfmrCode=CT50 buses=[106.2 s106b.1.0 s106b.0.2]
New Load.S106b         Bus1=s106b.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS107b  XfmrCode=CT50 buses=[107.2 s107b.1.0 s107b.0.2]
New Load.S107b         Bus1=s107b.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS109a  XfmrCode=CT50 buses=[109.1 s109a.1.0 s109a.0.2]
New Load.S109a         Bus1=s109a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS111a  XfmrCode=CT25 buses=[111.1 s111a.1.0 s111a.0.2]
New Load.S111a         Bus1=s111a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS112a  XfmrCode=CT25 buses=[112.1 s112a.1.0 s112a.0.2]
New Load.S112a         Bus1=s112a.1.2   Phases=2 Conn=Wye   Model=5 kV=0.208   kW=20.0  kvar=10.0
New Transformer.XFS113a  XfmrCode=CT50 buses=[113.1 s113a.1.0 s113a.0.2]
New Load.S113a         Bus1=s113a.1.2   Phases=2 Conn=Wye   Model=2 kV=0.208   kW=40.0  kvar=20.0
New Transformer.XFS114a  XfmrCode=CT25 buses=[114.1 s114a.1.0 s114a.0.2]
New Load.S114a         Bus1=s114a.1.2   Phases=2 Conn=Wye   Model=1 kV=0.208   kW=20.0  kvar=10.0

// redirect pvsystems_high_pvs.dss

! All devices in the test feeder are now defined.
!
! Many of the voltages are reported in per unit, so it is important to establish the base voltages at each bus so
! that we can compare with the result with greater ease.
! We will let the DSS compute the voltage bases by doing a zero-load power flow.
! There are only two voltage bases in the problem: 4160V and 480V. These must be expressed in kV
! We added 0.208 kV for split-secondary load buses

Set VoltageBases = [4.16 0.48 0.208]    ! ARRAY OF VOLTAGES IN KV
CalcVoltageBases  ! PERFORMS ZERO LOAD POWER FLOW TO ESTIMATE VOLTAGE BASES

Set MaxControlIter=30

New EnergyMeter.Feeder Line.L115 1

batchedit load..* model=1

solve
Buscoords buscoords.dat   ! load in bus coordinates
